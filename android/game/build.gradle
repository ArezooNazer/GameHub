plugins {
    id 'com.android.library'
    id 'maven-publish'
}
def gameHubVersion = '1.0.0'
def gameHubArtifact = 'alpha-3'
def gameHubArtifactDebug = 'alpha-3-debug'

android {
    compileSdk 30

    defaultConfig {
        minSdk 16
        targetSdk 29
        versionCode 1
        versionName "1.0"

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        debug{
            buildConfigField "String", "GAMEHUB_VERSION", '"' + gameHubVersion + '-' + gameHubArtifactDebug + '"'
        }
        release {
            minifyEnabled false
            buildConfigField "String", "GAMEHUB_VERSION", '"' + gameHubVersion + '-' + gameHubArtifact + '"'
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
}

// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'com.farsitel.bazaar.gamehub'
                artifactId = gameHubArtifact
                version = gameHubVersion
            }
            // Creates a Maven publication called “debug”.
            debug(MavenPublication) {
                // Applies the component for the debug build variant.
                from components.debug

                groupId = 'com.farsitel.bazaar.gamehub'
                artifactId = gameHubArtifactDebug
                version = gameHubVersion
            }
        }
    }
}